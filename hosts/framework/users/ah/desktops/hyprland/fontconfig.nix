{ config, lib, pkgs, ... }: {
  # Allow fontconfig to discover installed fonts. It runs `fc-cache -f`.
  fonts.fontconfig.enable = true;

  # Use fonts in personal config as default fonts in fontconfig. Until
  # https://github.com/nix-community/home-manager/pull/2732 gets merged.
  xdg.configFile = {
    "fontconfig/conf.d/52-hm-default-fonts.conf".text = let
      genDefault = fonts: name:
        lib.optionalString (fonts != [ ]) ''
          <alias binding="same">
            <family>${name}</family>
            <prefer>
            ${
              lib.concatStringsSep "" (map (font: ''
                <family>${font}</family>
              '') fonts)
            }
            </prefer>
          </alias>
        '';
    in ''
      <?xml version='1.0'?>

      <!-- Generated by Home Manager. -->

      <!DOCTYPE fontconfig SYSTEM 'urn:fontconfig:fonts.dtd'>
      <fontconfig>
        <!-- Default fonts -->
        ${genDefault [ config.lib.fonts.sans.name ] "sans-serif"}
        ${genDefault [ config.lib.fonts.serif.name ] "serif"}
        ${genDefault [ config.lib.fonts.mono.name ] "monospace"}
      </fontconfig>
    '';
  };
}
